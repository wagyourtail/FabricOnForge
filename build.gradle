plugins {
    id 'java'
    id 'dev.architectury.loom' version '0.8.0-SNAPSHOT'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group + ".FORGE-" + project.minecraft_version.replaceAll("\\.", "_")

repositories {
    mavenCentral()
    maven {
        name "Modmuss50 Repository"
        url 'https://maven.fabricmc.net/'
    }
    maven { url 'https://jitpack.io' }
}

minecraft {
}

loom {
    useFabricMixin = true
}

configurations {
    shadow
}

shadowJar {
    configurations = [project.configurations.shadow]
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "de.oceanlabs.mcp:mcp_snapshot:20201028-1.16.3"

    implementation 'net.fabricmc:tiny-mappings-parser:0.2.2.14'
    implementation 'net.fabricmc:tiny-remapper:0.4.2'
    shadow 'net.fabricmc:tiny-mappings-parser:0.2.2.14'
    shadow 'net.fabricmc:tiny-remapper:0.4.2'

    implementation 'org.ow2.sat4j:org.ow2.sat4j.core:2.3.6'
    implementation 'org.ow2.sat4j:org.ow2.sat4j.pb:2.3.6'
    shadow 'org.ow2.sat4j:org.ow2.sat4j.core:2.3.6'
    shadow 'org.ow2.sat4j:org.ow2.sat4j.pb:2.3.6'

    //nullable not defined so...
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
    inputs.property "META-INF/mods.toml", project.version

    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"

    //TODO: remove in 1.17?
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
    manifest {
        attributes([
            'MixinConnector': 'xyz.wagyourtail.fabriconforge.FabricLoaderEarlyRiser',
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}